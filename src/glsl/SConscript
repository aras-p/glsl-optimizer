import common

Import('*')

from sys import executable as python_cmd

env = env.Clone()

env.Prepend(CPPPATH = [
    '#include',
    '#src/mapi',
    '#src/mesa',
    '#src/glsl',
    '#src/glsl/glcpp',
])

env.Append(YACCFLAGS = '-d')

parser_env = env.Clone();
parser_env.Append(YACCFLAGS = ['--defines=src/glsl/glsl_parser.h', '-p', '_mesa_glsl_'])

glcpp_lexer = env.CFile('glcpp/glcpp-lex.c', 'glcpp/glcpp-lex.l')
glcpp_parser = env.CFile('glcpp/glcpp-parse.c', 'glcpp/glcpp-parse.y')
glsl_lexer = parser_env.CXXFile('glsl_lexer.cpp', 'glsl_lexer.ll')
glsl_parser = parser_env.CXXFile('glsl_parser.cpp', 'glsl_parser.yy')

# Make glcpp/glcpp-parse.h reacheable from the include path
env.Append(CPPPATH = [glcpp_parser[0].dir.up()])

sources = [
    glcpp_lexer,
    glcpp_parser[0],
    'glcpp/pp.c',
    'ast_expr.cpp',
    'ast_function.cpp',
    'ast_to_hir.cpp',
    'ast_type.cpp',
    glsl_lexer,
    glsl_parser[0],
    'glsl_parser_extras.cpp',
    'glsl_types.cpp',
    'glsl_symbol_table.cpp',
    'hir_field_selection.cpp',
    'ir_basic_block.cpp',
    'ir_clone.cpp',
    'ir_constant_expression.cpp',
    'ir.cpp',
    'ir_expression_flattening.cpp',
    'ir_function_can_inline.cpp',
    'ir_function.cpp',
    'ir_hierarchical_visitor.cpp',
    'ir_hv_accept.cpp',
    'ir_import_prototypes.cpp',
    'ir_print_visitor.cpp',
    'ir_reader.cpp',
    'ir_rvalue_visitor.cpp',
    'ir_set_program_inouts.cpp',
    'ir_validate.cpp',
    'ir_variable.cpp',
    'ir_variable_refcount.cpp',
    'linker.cpp',
    'link_functions.cpp',
    'loop_analysis.cpp',
    'loop_controls.cpp',
    'loop_unroll.cpp',
    'lower_discard.cpp',
    'lower_if_to_cond_assign.cpp',
    'lower_instructions.cpp',
    'lower_jumps.cpp',
    'lower_mat_op_to_vec.cpp',
    'lower_noise.cpp',
    'lower_variable_index_to_cond_assign.cpp',
    'lower_vec_index_to_cond_assign.cpp',
    'lower_vec_index_to_swizzle.cpp',
    'lower_vector.cpp',
    'opt_algebraic.cpp',
    'opt_constant_folding.cpp',
    'opt_constant_propagation.cpp',
    'opt_constant_variable.cpp',
    'opt_copy_propagation.cpp',
    'opt_copy_propagation_elements.cpp',
    'opt_dead_code.cpp',
    'opt_dead_code_local.cpp',
    'opt_dead_functions.cpp',
    'opt_discard_simplification.cpp',
    'opt_function_inlining.cpp',
    'opt_if_simplification.cpp',
    'opt_noop_swizzle.cpp',
    'opt_redundant_jumps.cpp',
    'opt_structure_splitting.cpp',
    'opt_swizzle_swizzle.cpp',
    'opt_tree_grafting.cpp',
    'ralloc.c',
    's_expression.cpp',
    'strtod.c',
] 


if env['crosscompile'] and env['platform'] != 'embedded':
    Import('builtin_glsl_function')
else:
    if env['msvc']:
        env.Prepend(CPPPATH = ['#/src/getopt'])
        env.PrependUnique(LIBS = [getopt])

    builtin_compiler = env.Program(
        target = 'builtin_compiler',
        source = sources + ['main.cpp', 'builtin_stubs.cpp',
                            '#src/mesa/program/hash_table.c',
                            '#src/mesa/program/symbol_table.c'],
    )

    builtin_glsl_function = env.CodeGenerate(
        target = 'builtin_function.cpp',
        script = 'builtins/tools/generate_builtins.py',
        source = builtin_compiler,
        command = python_cmd + ' $SCRIPT $SOURCE > $TARGET'
    )

    env.Depends(builtin_glsl_function, ['builtins/tools/generate_builtins.py', 'builtins/tools/texture_builtins.py'] + Glob('builtins/ir/*'))

    Export('builtin_glsl_function')

    if env['hostonly']:
        Return()


sources += builtin_glsl_function

glsl = env.ConvenienceLibrary(
    target = 'glsl',
    source = sources,
)

Export('glsl')

# FIXME: We can't build the programs because there's a cyclic dependency between tis directory and src/mesa
Return()

env = env.Clone()

if env['platform'] == 'windows':
    env.PrependUnique(LIBS = [
        'user32',
    ])

env.Prepend(LIBS = [glsl])

env.Program(
    target = 'glsl2',
    source = [
        'main.cpp',
    ]
)

env.Program(
    target = 'glcpp',
    source = ['glcpp/glcpp.c'],
)
