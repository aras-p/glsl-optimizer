import os

Import('*')

env = env.Clone()

env.Append(CPPPATH = [
    '#src/mapi',
    '#src/mesa',
    '.',
])

env.AppendUnique(CPPDEFINES = [
    '_GDI32_', # prevent wgl* being declared __declspec(dllimport)
    'BUILD_GL32', # declare gl* as __declspec(dllexport) in Mesa headers 
    'WIN32_LEAN_AND_MEAN', # http://msdn2.microsoft.com/en-us/library/6dwk3a1z.aspx
])
if not env['gles']:
    # prevent _glapi_* from being declared __declspec(dllimport)
    env.Append(CPPDEFINES = ['_GLAPI_NO_EXPORTS'])

wgl = env.ConvenienceLibrary(
    target ='wgl',
    source = env.ParseSourceList('Makefile.sources', 'C_SOURCES'),
)
Export('wgl')
