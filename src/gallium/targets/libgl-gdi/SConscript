#######################################################################
# SConscript for gdi winsys

Import('*')

env = env.Clone()

env.Append(CPPPATH = [
    '#src/gallium/state_trackers/wgl',
    '#src/gallium/winsys/sw',
])

env.Append(LIBS = [
    'gdi32',
    'user32',
    'kernel32',
    'ws2_32',
    talloc,
])

sources = ['libgl_gdi.c']
drivers = []

if True:
    drivers += [softpipe]

if env['llvm']:
    env.Append(CPPDEFINES = 'HAVE_LLVMPIPE')
    drivers += [llvmpipe]

if env['gcc']:
    sources += ['#src/gallium/state_trackers/wgl/opengl32.mingw.def']
else:
    sources += ['#src/gallium/state_trackers/wgl/opengl32.def']
    
drivers += [trace, rbug]

env['no_import_lib'] = 1

opengl32 = env.SharedLibrary(
    target ='opengl32',
    source = sources,
    LIBS = wgl + ws_gdi + glapi + mesa + drivers + gallium + glsl + env['LIBS'],
)

env.Alias('opengl32', opengl32)
env.Alias('libgl-gdi', opengl32)
